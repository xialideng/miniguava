// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'idea'

allprojects {
    repositories {
        jcenter()
    }

    project.plugins.whenPluginAdded { plugin ->
        if (plugin instanceof JavaPlugin) {
            project.sourceCompatibility = JavaVersion.VERSION_1_6
            project.targetCompatibility = JavaVersion.VERSION_1_6
        }
    }
}

afterEvaluate {
    allprojects {
        if (project.name == 'dummy-android-module') return

        project.apply {
            apply plugin: 'com.novoda.bintray-release'

            publish {
                uploadName = 'miniguava'
                groupId = 'net.ypresto.miniguava'
                artifactId = project.name
                publishVersion = '19.0.0-beta1'
                licences = ['Apache-2.0']
                desc = 'Guava for minimalists and Android'
                website = 'https://github.com/ypresto/miniguava'
                autoPublish = false
                dryRun = false
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
